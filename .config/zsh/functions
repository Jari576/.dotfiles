#!/bin/sh

source "$ZDOTDIR/key-bindings.zsh"

# add, commit and push
function send () {
    git add -A
    git commit -m "$1" || git commit && git push
}

# terminal spotify client
function spot () {
    # launch spotifyd if it's not running
    [ "$(pgrep "spotifyd")" ] || spotifyd
    spt
}

function toggleParuInstalled () {
    paru -Rp $1 &> /dev/null && paru -Rnsuvc $1 && return
    paru -Si $1 &> /dev/null && paru -S $1 && return
    echo "package named ${1} not found"
}

function fuzpac () {
    fzf --bind 'enter:execute(source ~/.config/zsh/functions && toggleParuInstalled {2})+reload(paru -${1-S}l)' --exact --border --no-sort --ansi --reverse --with-nth=1,2,4 --preview 'paru -Si {2}'
}

function initramfs () {

    kernel_dir="/usr/lib/modules"

    for kernel in $(\ls $kernel_dir)
    do
        kernel_name="$(cat $kernel_dir/$kernel/pkgbase)"
		sudo install -Dm0644 "$kernel_dir/$kernel/vmlinuz" "/boot/vmlinuz-$kernel_name"
		sudo dracut --force --hostonly "/boot/initramfs-$kernel_name.img" --kver "$kernel"
		sudo dracut --force --no-hostonly "/boot/initramfs-$kernel_name-fallback.img" --kver "$kernel"
    done

    sudo grub-mkconfig -o /boot/grub/grub.cfg
}

#################################
############ modules #############
##################################
function lfcd () {
    zle reset-prompt
    tmp="$(mktemp)"
    lf -last-dir-path="$tmp" "$@"
    if [ -f "$tmp" ]; then
        dir="$(cat "$tmp")"
        rm -f "$tmp"
        [ -d "$dir" ] && [ "$dir" != "$(pwd)" ] && cd "$dir"
    fi
    zle reset-prompt
    setTermCurrentDir
}
zle -N lfcd

function fzfcd () {
    zle reset-prompt
    setTerm "fzfcd (%~)"
    previewer='~/.config/lf/previewer {} $FZF_PREVIEW_COLUMNS $FZF_PREVIEW_LINES $(($COLUMNS - $FZF_PREVIEW_COLUMNS)) $(($LINES - $FZF_PREVIEW_LINES))'
    res=$(find . | fzf --height 40% --exact --reverse --nth=-1 --delimiter='/' --preview=${previewer})
    if [ -d "$res" ]; then
        cd "$res"
    elif [ -e "$res" ]; then
        cd "$(dirname $res)"
    fi
    zle reset-prompt
    setTermCurrentDir
}
zle -N fzfcd

function editCurDir () {
    $EDITOR .    
}
zle -N editCurDir

##################################
############# hooks $#############
##################################
# set tab name
function setTerm () {
    [[ -t 1 ]] || return
    case $TERM in
      sun-cmd) print -Pn "\e]l$1\e\\"
        ;;
      *xterm*|rxvt|(dt|k|E)term) print -Pn "\e]2;$1\a"
        ;;
    esac
}

function setTermCurrentDir () {
    setTerm %~
}

autoload -Uz add-zsh-hook
add-zsh-hook preexec setTerm
add-zsh-hook chpwd setTermCurrentDir
add-zsh-hook precmd setTermCurrentDir

